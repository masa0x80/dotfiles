#!/bin/zsh

tmpl_dir="$ZDOTDIR/tmpl"
zsh_cache="$XDG_CACHE_HOME/zsh-cache.zsh"
zshrc="$ZDOTDIR/.zshrc"

echo -n > $zsh_cache

# homebrew
brew shellenv >> $zsh_cache
source $zsh_cache

# misc
(( ${+commands[mise]} )) && mise mise activate zsh >> $zsh_cache
(( ${+commands[fzf]} )) && fzf --zsh >> $zsh_cache
(( ${+commands[direnv]} )) && direnv hook zsh >> $zsh_cache
(( ${+commands[starship]} )) && starship init zsh >> $zsh_cache

echo -n > $zshrc

cat $tmpl_dir/init.zsh >> $zshrc

# sheldon
sheldon source >> $zshrc

for file (
  $tmpl_dir/check-envrc-exist.zsh(N)
  $tmpl_dir/check-local-git-config.zsh(N)
  $tmpl_dir/complete-ssh-host.zsh(N)
  $tmpl_dir/completion.zsh(N)
  $tmpl_dir/dotdot.zsh(N)
  $tmpl_dir/fzf.zsh(N)
  $tmpl_dir/ghq.zsh(N)
  $tmpl_dir/git-add.zsh(N)
  $tmpl_dir/git-branch.zsh(N)
  $tmpl_dir/key-bindings.zsh(N)
  $tmpl_dir/navi.zsh(N)
  $tmpl_dir/prompt.zsh(N)
  $tmpl_dir/r.zsh(N)

  # Load gcloud
  $HOMEBREW_PREFIX/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/path.zsh.inc(N)
  $HOMEBREW_PREFIX/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/completion.zsh.inc(N)
) cat $file >> $zshrc

# zabrze
zabrze init --bind-keys >> $zshrc

for file (
  # Load local configurations
  $HOME/.config.local/zsh/zshrc(N)
  $HOME/.zshrc.local(N)
) cat $file >> $zshrc

cat $tmpl_dir/local-bin.zsh >> $zshrc

# zcompile
for file (
  $zshrc
  $HOME/.zshenv(N)
  $HOME/.config.local/zsh/zshenv(N)
  $HOME/.zshenv.local(N)
) zcompile $file
